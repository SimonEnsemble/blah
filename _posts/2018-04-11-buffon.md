---
layout: post
title: Monte Carlo simulation of Buffon's Needle
snippet: Modeling the probability of a needle intersecting a line when randomly dropped on a floor exhibiting infinite, equally spaced parallel lines. Uses analytical, geometrical, and simulation approaches to find the probability of a needle intersecting a line on the floor. 
tags: [simulation, computer programming]
author: Arthur York, Cory Simon
---

Buffon's Needle is a famous probability problem emanating from the 18th century. 

Imagine a floor marked with an infinite number of parallel, equidistant lines, a width $\ell$ apart. 
<figure>
    <img src="/images/Buffon/floor.png" alt="image" style="width: 100%;">
</figure>

We now toss a needle of length $L<\ell$ onto the floor, where it lands at a uniform random position and with a uniform random orientation. Buffon's needle problem is to determine the probability that the needle intersects one of the lines on the floor.
The probability depends on the ratio of the length of the needle, $L$, to the distance between the lines on the floor, $\ell$. 

In this post, we (i) derive the analytical solution to Buffon's needle problem and (ii) conduct a Monte Carlo simulation of needle tossing in the Julia programming language to estimate the probability that a needle crosses a line. Below is an outcome from our simulation, where the needles are the teal-colored lines.

<figure>
    <img src="/images/Buffon/buffon.png" alt="image" style="width: 100%;">
</figure>


## representing the floor

We create a data structure `Floor` to represent the floor, characterized by the distance $\ell$ between successive parallel lines, stretching in the $y$-direction and repeating ad infinitum in the $x$-direction.

```julia
struct Floor
	ℓ::Float64
end
```

## representing a needle

The position and orientation of a needle on the floor is described by the Cartesian coordinates $(x,y)$ of its center and the angle $\theta$ it makes with the vertical axis. 

<figure>
    <img src="/images/Buffon/needle_diagram.png" alt="image" style="width: 100%;">
</figure>

Note that the $y$ coordinate of a needle does not influence whether it intersects a line on the floor or not.
Therefore, we do not need to consider it for our calculations; we only need to keep track of the $x$-coordinate (green axis is the $x$-axis) and angle $\theta$ of a needle during the simulation.

We create a data structure `Needle` to represent a needle, storing its state $(x, \theta)$ and its attribute $L$.

```julia
struct Needle
	x::Float64
	θ::Float64
	L::Float64
end
```

#### the state space of a needle

The simulation needs to model a needle being dropped anywhere on an infinite floor, but it is impractical to create an infinite floor for the simulation. If we define two lines and the space between them as a chunk, we can see that the fraction of needles that intersect a line in each chunk is expected to be the same for each chunk on the floor. Thus for our simulation, we consider only a single chunk $0 \leq x \leq \ell$ and arrive at the answer to Buffon's needle problem, which concerns an infinite floor.

It would be proper to allow the angle with the vertical $0 \leq \theta \leq 2\pi$, however we can take advantage of some rotational symmetry. A needle with angle $\theta$ appears equivalent to a needle with the same coordinates and angle of $\theta + \pi$. We therefore only allow $0 \leq \theta \leq \pi$ in our calculations. For example, since $0 \leq \theta \leq \pi$ contains $\frac{\pi}{4}$ there is no need to test $\frac{5\pi}{4}$ because they would describe the same orientation of a needle.

The "state space" $\mathcal{R}$ of a needle, and therefore the simulation box for our simulation is:

$$\mathcal{R} = \left \{ (x,\theta) \mid 0\leq x\leq \ell,0\leq \theta \leq \pi \right \}$$

Randomly throwing a needle on the floor in our simulation is equivalent to drawing a uniformly distributed sample from the state space $\mathcal{R}$.

## simulation of needle tosses

For the simulation, we created a `toss_needle` function that returns a `Needle` uniformly distributed in the state space. 
The function takes in as arguments the length of the needle, $L$, to toss, and the floor, `floor`, on which the needle is tossed.

```julia
function toss_needle(L::Float64, floor::Floor)
	x = rand() * floor.ℓ
	θ = rand() * π
	return Needle(x, θ, L)
end
```

This function essentially "samples" the needle state space, $\mathcal{R}$.

## checking a needle for intersection with a line on the floor

A needle will intersect a line if part of the needle overlaps $x=0$ or $x=\ell$. We check for overlap by checking the $x$-coordinates of the endpoints of the needles. If one of those is outside the simulation box (one is less than $0$ or greater than $\ell$), then the needle intersects a line. 

The $x$-coordinates of the two endpoints of a needle are found as follows. First, take the $x$-coordinate for the center of the needle. Then add or subtract half of the length of the projection of the needle onto the $x$-axis, $\frac{L}{2}\sin\theta$. This generates two conditions for whether or not a needle crosses a line. A needle with coordinate $x$ and angle $\theta$ intersects a line if and only if:

<center>$x+\frac{L}{2}\sin\theta\geq \ell$ or $x-\frac{L}{2}\sin\theta\leq 0$</center>

The function `cross_line` takes a `Needle` and a `Floor` as arguments and returns `true` iff the needle intersects with a line on the `floor`.

```julia
function cross_line(needle::Needle, floor::Floor)
	x_right_tip = needle.x + needle.L / 2 * sin(needle.θ)
	x_left_tip  = needle.x - needle.L / 2 * sin(needle.θ)
	return x_right_tip > floor.ℓ || x_left_tip < 0.0
end
```

## obtaining the probability analytically

To analytically obtain the probability of a needle intersecting a line, we can look at the fraction of the state space that leads to a needle intersecting a line. Visually, we can graph the state space $\mathcal{R}$ of the needle in the $(x,\theta)$ plane and shade the regions of state space that result in needles intersecting lines. These regions are described by $x+\frac{L}{2}\sin\theta>\ell$ and $x-\frac{L}{2}\sin\theta<0$. The total areas of these regions divided by the total area of the state space is then the probability of a needle intersecting a line. Below, the regions of state space that result in an intersection are shown in blue, whereas needles that fall in the green region do not intersect a line on the floor.

<figure>
    <img src="/images/Buffon/state_space_no_needles.png" alt="image" style="width: 100%;">
</figure>

The area of the total state space of the needle is:

<center>$$A_T=\int_{0}^{\pi}\int_0^{\ell} dx d\theta=\ell\pi$$</center>

The area of the state space satisfying $x-\frac{L}{2}\sin\theta\leq0$ is:

<center>$$A_1=\int_{0}^{\pi}\left(\frac{L}{2}\sin\theta\right)d\theta=L$$</center>

The area of the state space satifying $x+\frac{L}{2}\sin\theta\geq \ell$ is:

<center>$$A_2=\int_{0}^{\pi}\ell d\theta-\int_{0}^{\pi}\left(\ell-\frac{L}{2}\sin\theta\right)d\theta=\int_{0}^{\pi}\left(\frac{L}{2}\sin\theta\right)d\theta=L$$</center>

Therefore, the total area of needle state space that results in intersection with a line on the floor is:

<center>$A_1+A_2=L+L=2L$</center>

and thus the probability of a needle landing on a line is the fraction of state space that is colored blue:

<center>$\dfrac{A_1+A_2}{A_T}=\dfrac{2L}{\ell\pi}$</center>

Intruigingly, one could compute the inverse of $\pi$ by throwing needles on a floor if $L=\ell/2$.

## estimating the probability through a Monte Carlo simulation

To calculate Buffon's needle probability with a Monte Carlo simulation, we will toss needles on the floor with our function `toss_needle`, which draws a uniformly random positions $x$ and uniformly random orientation (angle) $\theta$ to decide where the needle lands on the floor. This is effectively sampling a point from the needle state space $\mathcal{R}$. We then keep track of the fraction of the needles that intersected a line as an estimate of Buffon's needle probability. We expect the fraction of needles intersecting a line in the simulation to be $2L/(\ell \pi)$.

```julia
function estimate_prob_needle_crosses_line(nb_tosses::Int, 
                                           floor::Floor, 
                                           L::Float64)
	nb_crosses = 0
	for t = 1:nb_tosses
		needle = toss_needle(L, floor)
		if cross_line(needle, floor)
			nb_crosses += 1
		end
	end
    # return fraction of needles that cross a line
	return nb_crosses / nb_tosses 
end
```

We plot randomly generated needles as points in their state space $\mathcal{R}$ below. The needles that were found to intersect a line on the floor are colored with blue. Those that didn't intersect a line are colored green. This visualization is valuable because, once we plot the data, the curves $x+\frac{L}{2}\sin\theta\geq \ell$ and $x-\frac{L}{2}\sin\theta\leq 0$ and their areas $A_2$ and $A_1$ from the analytical approach are apparent. Also note that if $\theta$ is close to zero or $\pi$, it is highly unlikely (more red) that a needle will intersect a line because it is almost parallel with the lines on the floor.

<figure>
    <img src="/images/Buffon/state_space.png" alt="image" style="width: 100%;">
</figure>

We compared the estimated probability from the Monte Carlo simulation to the theoretical probability $\frac{2L}{\ell\pi}$, and they are very close when the number of tosses used to estimate the probability is large.
:thumbsup:

The plot below shows the estimated probability-- the fraction of needles that intersect a line in a simulation-- as we change the number of tosses used. Here, $\ell=2$ and $L=1$.
The dots show the average estimation among 5,000 simulations for the fixed number of tosses, which agrees with the theoretical probability, $\frac{1}{\pi}$, shown as the dashed, horizontal line.
The error bars include the middle 90% of the estimates. As we use more tosses to estimate the probability of intersection, the error bars shrink, meaning that the estimate exhibits less variance.

<figure>
    <img src="/images/Buffon/errorbars.png" alt="image" style="width: 100%;">
</figure>
